# SWIG Java Binding
FUNCTION(SWIG_JAVA_WRAPPER
    C_LIBRARY
    CJ_INTERFACE
    J_LIBRARY
    J_PACKAGE
)

SET(EXTRA_DEPS ${ARGV4})
SET(EXTRA_FLAGS ${ARGV5})

STRING(REGEX REPLACE "\\." "/" J_PACKAGE_DIR ${J_PACKAGE})

ADD_DEFINITIONS(-DSWIG_BUILD)

FIND_PACKAGE(SWIG REQUIRED)
INCLUDE(${SWIG_USE_FILE})

FIND_PACKAGE(Java)
FIND_PACKAGE(JNI)
INCLUDE_DIRECTORIES(${JAVA_INCLUDE_PATH} ${JAVA_INCLUDE_PATH}/linux)

SET(CMAKE_SWIG_FLAGS "")

FILE(MAKE_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/${J_PACKAGE_DIR}")
SET(CMAKE_SWIG_OUTDIR "${CMAKE_CURRENT_BINARY_DIR}/${J_PACKAGE_DIR}")
SET(SWIG_MODULE_${J_LIBRARY}_EXTRA_DEPS "${EXTRA_DEPS};${C_LIBRARY}")
SET_SOURCE_FILES_PROPERTIES(${CJ_INTERFACE} PROPERTIES CPLUSPLUS ON)
SET_SOURCE_FILES_PROPERTIES(${CJ_INTERFACE} PROPERTIES SWIG_FLAGS "-package;${J_PACKAGE};${EXTRA_FLAGS}")

SWIG_ADD_MODULE(${J_LIBRARY} java ${CJ_INTERFACE})
SWIG_LINK_LIBRARIES(${J_LIBRARY} ${C_LIBRARY})

# ! Compiling the JNI library with -O2 or -O3 causes NullPointerExceptions!
SET_SOURCE_FILES_PROPERTIES(${swig_generated_file_fullname} COMPILE_FLAGS -O1)

ADD_CUSTOM_COMMAND(
    OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/${J_LIBRARY}.jar
    COMMAND ${JAVA_COMPILE} -O -d . ../../${J_PACKAGE_DIR}/*.java
    COMMAND ${JAVA_ARCHIVE} cf ../../${J_LIBRARY}.jar ${J_PACKAGE_DIR}
    DEPENDS ${J_LIBRARY}
    COMMENT "Compiling java files and building jar library.."
    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/CMakeFiles/${J_LIBRARY}-buildjar.dir
)

SET_TARGET_PROPERTIES(${J_LIBRARY} PROPERTIES PREFIX lib)

IF(CMAKE_SYSTEM_NAME STREQUAL Darwin)
    SET_TARGET_PROPERTIES(${J_LIBRARY} PROPERTIES SUFFIX .dylib)
ENDIF(CMAKE_SYSTEM_NAME STREQUAL Darwin)

IF(CMAKE_SYSTEM_NAME STREQUAL Linux)
    SET_TARGET_PROPERTIES(${J_LIBRARY} PROPERTIES LINK_FLAGS
                          -Wl,-no-undefined -module -avoid-version)
ENDIF(CMAKE_SYSTEM_NAME STREQUAL Linux)

ADD_CUSTOM_TARGET(${J_LIBRARY}-buildjar ALL
    DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/${J_LIBRARY}.jar)

INSTALL(TARGETS ${J_LIBRARY} DESTINATION lib)

INSTALL(FILES ${CMAKE_CURRENT_BINARY_DIR}/${J_LIBRARY}.jar DESTINATION java-jar-package)

ENDFUNCTION(SWIG_JAVA_WRAPPER)
